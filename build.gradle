buildscript {
    repositories {
        maven {
            url "https://files.minecraftforge.net/maven"
        }
        mavenCentral()
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "5.1.+", changing: true
    }
}

apply plugin: "net.minecraftforge.gradle"

version = "1.5.32"
group = "com.bobmowzie.mowziesmobs"
archivesBaseName = "mowziesmobs"
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: "official", version: "1.18.2"
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            workingDirectory project.file("run")
            property "forge.logging.console.level", "debug"
            mods {
                mowziesmobs {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file("run-server")
            property "forge.logging.console.level", "debug"
            mods {
                mowziesmobs {
                    source sourceSets.main
                }
            }
        }
    }
}

processResources {
    filesMatching("META-INF/mods.toml") {
        expand([
                'version': "${project.version}"
        ])
    }
}

jar {
    manifest {
        attributes([
            "Specification-Title": "mowziesmobs",
            "Specification-Vendor": "BobMowzie",
            "Specification-Version": "1",
            "Implementation-Title": "mowziesmobs",
            "Implementation-Version": "${project.version}",
            "Implementation-Vendor" : "BobMowzie",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

repositories {
    maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
    maven { url "https://www.cursemaven.com" }
}

dependencies {
    minecraft "net.minecraftforge:forge:1.18.2-40.1.73"
    implementation fg.deobf('software.bernie.geckolib:geckolib-forge-1.18:3.0.56')
    implementation fg.deobf("curse.maven:worldedit-225608:3697298")
}